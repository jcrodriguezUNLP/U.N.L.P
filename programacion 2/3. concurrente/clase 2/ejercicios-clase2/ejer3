programa ejemplo
procesos
  proceso contarFlores_Y_Papeles ( ES flores : numero ; ES papeles : numero )
  comenzar
    mientras ( HayPapelEnLaEsquina )
      tomarPapel
      papeles := ( papeles + 1 )
    mientras ( HayFlorEnLaEsquina )
      tomarFlor
      flores := ( flores + 1 )
    
    mientras ( HayFlorEnLaBolsa )
      depositarFlor
    mientras ( HayPapelEnLaBolsa )
      depositarPapel
  fin

  proceso escalera( E escalones : numero ; E incrementar : numero )
  variables
    cantEsquinas : numero
    largo        : numero
    flores       : numero
    papeles      : numero

  comenzar
    cantEsquinas := 0
    largo        := incrementar

    repetir ( escalones )
      flores  := 0
      papeles := 0

      repetir ( largo )
        contarFlores_Y_Papeles( flores , papeles )
        mover

      derecha

      repetir ( largo )
        contarFlores_Y_Papeles( flores , papeles )
        mover
      
      contarFlores_Y_Papeles( flores , papeles )
      
      repetir ( 3 )
        derecha
      
      largo := ( largo + incrementar )

      si ( (papeles - 1) = flores )
        cantEsquinas := ( cantEsquinas + 1 )

    Informar( 'Escalones con un papel mas q flores' , cantEsquinas )
  fin

areas
  ciudad : AreaC( 1 , 1 , 100 , 100 )

robots
  robot robot1
  comenzar
    escalera( 4 , 1 )
  fin

variables
  R_info : robot1

comenzar
  AsignarArea ( R_info , ciudad )
  Iniciar     ( R_info , 1 , 1  )
fin