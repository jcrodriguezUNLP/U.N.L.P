programa ejer1
procesos
  proceso incrementar( ES x : numero )
  comenzar
    x := ( x + 1 )
  fin
  
  proceso tomarFlores( ES cantFlores : numero )
  comenzar
    mientras( HayFlorEnLaEsquina )
      tomarFlor
      incrementar( cantFlores )
  fin
  
  proceso tomarPapeles( ES cantPapeles : numero )
  comenzar
    mientras( HayPapelEnLaEsquina )
      tomarPapel
      incrementar( cantPapeles )
  fin
  
  proceso limpiarEsquina( ES cantFlores : numero ; ES cantPapeles : numero )
  comenzar
    tomarFlores  ( cantFlores  )
    tomarPapeles ( cantPapeles )
  fin
  
  proceso limpiarLinea( E largo : numero ; E flores : boolean ; ES cantFlores : numero ; E papeles : boolean ; ES cantPapeles : numero )
  comenzar
    si( flores & papeles )
      repetir( largo )
        limpiarEsquina( cantFlores , cantPapeles )
        mover
      limpiarEsquina( cantFlores , cantPapeles )
    sino
      si( flores )
        repetir( largo )
          tomarFlores( cantFlores )
          mover
        tomarFlores( cantFlores )
      sino
        repetir( largo )
          tomarPapeles( cantPapeles )
          mover
        tomarPapeles( cantPapeles )
  fin
  
areas
  ciudad: AreaC( 1 , 1 , 100 , 100 )

robots
  robot tipo1
  variables
    cantFlores  : numero
    cantPapeles : numero
    aux         : numero
  comenzar
    cantFlores  := 0
    cantPapeles := 0
    limpiarLinea( 9 , V , cantFlores , F , cantPapeles )
    EnviarMensaje  ( cantFlores , robot2 )
    RecibirMensaje ( aux        , robot2 )
    si( aux < cantFlores )
      Informar( 'Gane por' , (cantFlores - aux) )
  fin
  
  robot tipo2
  variables
    cantFlores  : numero
    cantPapeles : numero
    aux         : numero
  comenzar
    cantFlores  := 0
    cantPapeles := 0
    limpiarLinea( 9 , V , cantFlores , F , cantPapeles )
    EnviarMensaje  ( cantFlores , robot1 )
    RecibirMensaje ( aux        , robot1 )
    si( aux < cantFlores )
      Informar( 'Gane por' , (cantFlores - aux) )
  fin

variables
  robot1 : tipo1
  robot2 : tipo2

comenzar
  AsignarArea( robot1 , ciudad )
  AsignarArea( robot2 , ciudad )
  Iniciar( robot1 , 1 , 1  )
  Iniciar( robot2 , 2 , 11 )
fin
